//******************************************************************************
// YAYA　ゴーストテンプレート
// 種々のイベント辞書
//******************************************************************************

//******************************************************************************
//シェル切り替え
//******************************************************************************

//------------------------------------------------------------------------------
//シェルチェンジ開始
//------------------------------------------------------------------------------

OnShellChanging
{
	"\0\s[5]着替えまーす。\w8\1着替えだけで済むのか？\e"
}

//------------------------------------------------------------------------------
//シェルチェンジ完了
//------------------------------------------------------------------------------
OnShellChanged
{
	"\0\s[7]変身！%(reference[0])！\w8\s[5]\n似合うかな？\w8\1良い感じだな。\e"
}


//******************************************************************************
//インストール関連
//******************************************************************************

//------------------------------------------------------------------------------
//インストール中のメッセージ
//------------------------------------------------------------------------------
OnInstallBegin
{
	"\0\s[6]インストールしています‥\w6‥\w6\e"
}

//------------------------------------------------------------------------------
//インストール完了メッセージ
//------------------------------------------------------------------------------
OnInstallComplete
{
	_ref0 = SPLIT(reference[0],' with',2)[0] //hogehoge with hemohemo
	
	"\0\s[5]"
	--
	if _ref0 == "shell" {
		"シェル"
	}
	elseif _ref0 == "ghost" {
		"ゴースト"
	}
	elseif _ref0 == "balloon" {
		"バルーン"
	}
	elseif _ref0 == "plugin" {
		"プラグイン"
	}
	--
	"「%(reference[1])」、インストール完了ですよー。\n"
	--
	if basewarenameex == 'SSP' {
		_id = ''
		if _ref0 == "shell" {
			_id = 'shell'
		}
		elseif _ref0 == "ghost" {
			_id = 'ghost'
		}
		elseif _ref0 == "balloon" {
			_id = 'balloon'
		}
		
		if _id != '' {
			"切り替えますか？"
			--
			AYATEMPLATE.MenuItem("切り替える","OnAYATEMPLATE.InstallChange",_id,200)
			--
			AYATEMPLATE.MenuItem("切り替えない","Menu_CANCEL")
		}
	}
}

OnAYATEMPLATE.InstallChange
{
	"\![change,%(reference[0]),lastinstalled]"
}

//------------------------------------------------------------------------------
//インストール失敗メッセージ
//------------------------------------------------------------------------------
OnInstallFailure
{
	"\0\s[4]インストールに失敗しましたー‥\w5‥\w5\n\n理由：%(reference[0])。あーあ。\e"
	"\0\s[5]こちらは５階、%(reference[0])でございまーす。\w8\1つまりインストールに失敗したんだろ。\e"
}

//------------------------------------------------------------------------------
//インストール拒否（他のゴーストのためのアーカイブだった）
//------------------------------------------------------------------------------
OnInstallRefuse
{
	"\1\s[10]これは、%(reference[0])用のアーカイブだぞ。\w6\0\s[0]処理中断っ。\e"
	"\0\s[5]ぴーんぽーんぱーんぷー♪\w9\n%(reference[0])さん、お荷物が届いてまーす。\w8\1つまり%(reference[0])に渡してやってくれ。\e"
}


//******************************************************************************
//消滅関連
//******************************************************************************

//------------------------------------------------------------------------------
//消滅指示
//------------------------------------------------------------------------------
OnVanishSelecting
{
	"\0\s[4]そ、そんなぁ\w5‥\w5‥\1\s[10]あきらめるのはまだ早いぞ。\e"
}

//------------------------------------------------------------------------------
//消滅開始
//------------------------------------------------------------------------------
OnVanishSelected
{
	"\0\s[4]‥\w5‥\w5さようなら。\w8\1また会えることを楽しみにしてるぞ。\w8\0\n\n‥\w5‥\w5化けて出てやる。\w8\1\s[11]\n\nちょっ！\e"
}

//------------------------------------------------------------------------------
//消滅中止
//------------------------------------------------------------------------------
OnVanishCancel
{
	"\0\s[4]ぷー。\w8\1助かったな‥\w5‥\w5\w8\0\s[7]\n\n%(username)！冗談にもほどがあるよ！\w8\1\n\n果たして冗談だったのかな？\w8\0\s[4]\n\n‥\w5‥\w5そう信じたいよ。\e"
}

//------------------------------------------------------------------------------
//他のゴーストが消滅して自分に切り替わった
//------------------------------------------------------------------------------
OnVanished
{
	"\1\s[10]命ある物はいつか滅び、命無きゴーストもいつかは電子の海に消えゆく運命。\w8\0\s[1]どうしたのマック朗、熱暴走？\w8\1\n\nちゃうわ。\e"
	"\0\s[4]%(reference[0])さんが居なくなっちゃった‥\w5‥\w5\w8\1いつかまた会えるさ。\w8\0\n\n‥\w5‥\w5だよね。\e"
}


//******************************************************************************
//メールチェック
//******************************************************************************

//------------------------------------------------------------------------------
//メールチェック開始
//------------------------------------------------------------------------------
OnBIFFBegin
{
	"\0\s[5]メールが届いてないかチェックしますね。\n\nマック朗、よろしく。\1\s[10]んむ。\e"
}

//------------------------------------------------------------------------------
//メールチェック成功
//------------------------------------------------------------------------------
OnBIFFComplete
{
	if reference[0] == 0
	{
		//---- メールは無かった
		"\1\s[10]メールは無かったな。\w5\0\s[0]ご苦労さま。\n\n\s[1]‥\w5‥\w5%username。寂しい？\e"
	}
	else
	{
		//---- メールが届いていた
		"\1\s[10]%(reference[0])通のメールがあったぞ。\w5\0\s[5]%username、さっそく見てみたら？\e"
	}
}

//------------------------------------------------------------------------------
//メールチェック失敗
//------------------------------------------------------------------------------
OnBIFFFailure
{
	if reference[0] == "timeout"
	{
		//---- タイムアウト
		"\1\s[10]いつまで待ってもメールサーバにつながらないんだが。\w5\0\s[4]どうしたんだろう‥\w5‥\w5\e"
	}
	elseif reference[0] == "kick"
	{
		//---- 蹴られた
		"\1\s[10]蹴られたぞ。\0\s[1]おかしいね‥\w5‥\w5なんでだろ。\e"
	}
	elseif reference[0] == "defect"
	{
		//---- 設定誤り
		"\1\s[10]なんか、ぜんぜんダメなんだが。\w5\0\s[4]メールチェックの設定を見なおしてね、%username。\e"
	}
	else
	{
		//---- 原因不明
		"\1\s[10]原因はわからないが、失敗。\w5\0\s[4]‥\w5‥\w5あれー？\e"
	}
}


//******************************************************************************
//ネットワーク更新
//******************************************************************************

//------------------------------------------------------------------------------
//ネットワーク更新開始
//------------------------------------------------------------------------------
OnUpdateBegin
{
	"\0\s[5]成長するサンプルー。\w8\1するのかなぁ？\w8\0\s[4]\n\n多分‥\w5‥\w5\e"
	"\0\s[0]ネットワークを介して頭がぐんぐん良くなるっ開始っ！\w5\1\s[10]ならねえよ。\e"
	"\0\s[0]ネットワークを介して胸がぐんぐん大きくなる開始っ！\w8\1絶対ないっ！！\e"
	"\0\s[5]ゆーけよ電子の海原へー♪\w9\n更新ファイルはすぐそこだー♪\w8\1なんの歌なんだよ。\e"
}

//------------------------------------------------------------------------------
//ネットワーク更新準備完了
//------------------------------------------------------------------------------
OnUpdateReady
{
	"\0\s[0]%(TOINT(reference[0])+1)個のファイルをダウンロードします。\w5\1\s[10]ＯＨ！\e"
	"\0\s[5]%(TOINT(reference[0])+1)個のファイルを%(username)のハードディスクに不法侵入させる開始ー。\w8\1その言い方やめろ。\e"
	"\0\s[5]おーきくなるよー。\w8\1頭がか。\w8\0\s[4]\n\n‥\w5‥\w5ぷー。\e"
}

//------------------------------------------------------------------------------
//ネットワーク更新終了
//------------------------------------------------------------------------------
OnUpdateComplete
{
	if reference[0] == "none"
	{
		//---- 更新すべきファイルが無かった
		"\1\s[10]あいや、盛り上がってるとこ水を差すようだが、対象ファイルが無いぞ。\w5\0\s[0]ぬあー。\e"
		"\0\s[4]‥\w5‥\w5きょ、きょうはだめでも‥\w5‥\w5\w9\nあしたはでっかい太陽が‥\w5‥\w5\w8\1パクんな。\e"
	"\0\s[4]ぷー。\w8\1‥\w5‥\w5まあ、そんな時もあるさ。\e"
	}
	else
	{
		//---- 実際に更新が完了した
		"\0\s[0]更新完了～。\w5\1\s[10]おつかれさん。\e"
		"\0\s[3]‥\w5‥\w5大きくなったかな。\w8\1辞書ファイルがな。\w8\0\s[4]\n\nぷー。\w8\1\n\n‥\w5‥\w5いや、何が悲しいんだ。\e"
		"\0\s[5]ぜんた～い、とまれっ。\w8\1えーと‥\w5‥\w5\w8\s[11]\nそれは行進終了だっ！\w8\0\s[7]\n\nマック朗、遅いっ！\e"
	}
}

//------------------------------------------------------------------------------
//ネットワーク更新失敗
//------------------------------------------------------------------------------
OnUpdateFailure
{
	if reference[0] == "timeout"
	{
		//---- タイムアウトした
		"\0\s[1]‥\w5‥\w5時間切れ？\w8\1どうしたんだろう。"
	}
	elseif reference[0] == "md5 miss"
	{
		//---- MD5値が一致しなかった
		"\u\s[10]\0\s[1]‥\w5‥\w5MD5値が違うんだって。\w8\1お手数だが、配布元に報告して貰えるとありがたいな。"
	}
	else
	{
		//---- 上記以外の理由で失敗した
		"\0\s[1]%(reference[0])、だって。\w5\1\s[10]うーん。"
	}
	--
	"\n\n\1というわけで、ネットワーク更新、失敗だ。\e"
}

//------------------------------------------------------------------------------
//ネットワーク更新 ダウンロード開始
//------------------------------------------------------------------------------
OnUpdate.OnDownloadBegin
{
	"\0\s[5]ファイル「%(reference[0])」をダウンロードします。\e"
}

//------------------------------------------------------------------------------
//ネットワーク更新 MD5値照合開始
//------------------------------------------------------------------------------
OnUpdate.OnMD5CompareBegin
{
	"\0\s[5]ファイルの確認ー。\w8\e"
	"\0\s[1]合ってるかな‥\w5‥\w5\w8\1\e"
}

//------------------------------------------------------------------------------
//ネットワーク更新 MD5値一致
//------------------------------------------------------------------------------
OnUpdate.OnMD5CompareComplete
{
	"\0\s[5]ん、オッケー。\e"
	"\0\s[5]いいみたいだね。\1\s[10]うむ。\e"
}

//------------------------------------------------------------------------------
//ネットワーク更新 MD5値不一致
//------------------------------------------------------------------------------
OnUpdate.OnMD5CompareFailure
{
	"\0\s[1]マック朗、ＭＤ５って何だっけ？\w8\1普通なら間違えることがない情報の事だ。\w8\0\s[4]\n\nつまり、間違えてるんだねこれ。\e"
	"\0\s[1]ぷー‥\w5‥\w5\w8\1なんだよ。\w8\0\n\nたぶんなんかがエラー。\w8\1\n\n多分って。\e"
}

//******************************************************************************
//ヘッドラインセンス
//******************************************************************************

//------------------------------------------------------------------------------
//ヘッドラインセンス開始
//------------------------------------------------------------------------------
OnHeadlinesenseBegin
{
	"\0\s[0]%(reference[0])ヘッドライン取得中。\e"
}

//------------------------------------------------------------------------------
//ヘッドライン読み上げ
//------------------------------------------------------------------------------
OnHeadlinesense.OnFind
{
	//---- はじめのセリフ
	if reference[2] == "First" || reference[2] == "First and Last"
	{
		//---- 最初のページ
		"\0\b2\s[0]%(reference[0])のヘッドラインですよっ。"
	}
	elseif reference[2] == "Next"
	{
		//---- 真ん中あたりのページ
		"\0\b2\s[0]%(reference[0])ヘッドライン、続き。"
	}
	elseif reference[2] == "Last"
	{
		//---- 最後のページ
		"\0\b2\s[0]%(reference[0])ヘッドライン、これが最後のページです。"
	}
	--
	//---- ヘッドライン本体
	"\n\n%(reference[3])\n\n"
	--
	//---- 選択肢
	if reference[2] == "First" || reference[2] == "Next"
	{
		//---- 次のページがある場合
		AYATEMPLATE.MenuItem("次のページ","")
		--
		AYATEMPLATE.MenuItem("サイトへ",reference[1])
		--
		AYATEMPLATE.MenuItem("やめ","Menu_HEADLINECANCEL",150)
	}
	else
	{
		//---- 次のページがない場合
		AYATEMPLATE.MenuItem("サイトへ",reference[1])
		--
		AYATEMPLATE.MenuItem("やめ","Menu_HEADLINECANCEL",150)
	}
}

//------------------------------------------------------------------------------
//ヘッドライン更新が無かった
//------------------------------------------------------------------------------
OnHeadlinesenseComplete
{
	"\0\s[0]%(reference[0])、更新はないみたいです。\e"
}

//------------------------------------------------------------------------------
//ヘッドラインセンス失敗
//------------------------------------------------------------------------------
OnHeadlinesenseFailure
{
	"\0\s[0]ヘッドラインセンスに失敗しました。\n\n理由："
	--
	if reference[0] == "can't download"
	{
		"ダウンロード失敗。"
	}
	elseif reference[0] == "can't analyze"
	{
		"ヘッドライン解析失敗。"
	}
	else
	{
		"謎。"
	}
	--
	"\e"
}


//******************************************************************************
//ファイル作成関連
//******************************************************************************

//------------------------------------------------------------------------------
//updates2.dau作成開始
//------------------------------------------------------------------------------
OnUpdatedataCreating
{
	"\0\s[5]アップデートデータを作成しています。\w8\1できるか？\w8\0\s[4]\n\n多分。\e"
}

//------------------------------------------------------------------------------
//updates2.dau作成完了
//------------------------------------------------------------------------------
OnUpdatedataCreated
{
	"\0\s[5]アップデートデータ、ちゃんと作れたよ。\w8\1よしよし。\e"
}

//------------------------------------------------------------------------------
//NAR作成開始
//------------------------------------------------------------------------------
OnNarCreating
{
	"\0\s[5]narファイルを作成するよ。\w8\1ちゃんとできるのか？\w8\0\n\nまかせて！\w8\1\n\n‥\w5‥\w5不安だなぁ。\e"
}

//------------------------------------------------------------------------------
//NAR作成完了
//------------------------------------------------------------------------------
OnNarCreated
{
	"\0\s[5]「%(reference[0])」のnarファイル、「%(reference[1])」が爽やかに完成！\w8\1爽やかにって。\e"
}

//******************************************************************************
//時計合わせ
//みか (http://homepage2.nifty.com/ko-ki/) の辞書を参考にしました。感謝＞こーき氏
//******************************************************************************

//------------------------------------------------------------------------------
//時計合わせ開始
//------------------------------------------------------------------------------
OnSNTPBegin
{
	"\0\s[5]時計、合わせまーす。\w8\1%(reference[0])に接続するぞ。\e"
}

//------------------------------------------------------------------------------
//時刻取得成功、比較開始
//この関数で 2008/03/25 19:15:05 のように見やすい形に作り変えます。
//詳しくはSTRFORM関数の説明を見てね！
//わからなければこのままで。
//------------------------------------------------------------------------------

SNTPCompare_StrForm
{
	_txt = SPLIT(_argv[0],',')
	STRFORM("$04d/$02d/$02d $02d:$02d:$02d",TOINT(_txt[0]),TOINT(_txt[1]),TOINT(_txt[2]),TOINT(_txt[3]),TOINT(_txt[4]),TOINT(_txt[5]))
}

OnSNTPCompare
{
	"\0\s[0]\_q/
	修正前 %(SNTPCompare_StrForm(reference[2]))\n/
	修正後 %(SNTPCompare_StrForm(reference[1]))\n/
	\_q\n"
	--
	if reference[3] == 0
	{
		"うーん、ぴったり。\e"
	}
	else
	{
		//\6は補正実行タグ。消しちゃだめ
		"\6\0\n\s[5]%(reference[3])秒のずれを修正したよ。"
	}
}

//------------------------------------------------------------------------------
//時計合わせ失敗
//------------------------------------------------------------------------------
OnSNTPFailure
{
	"\0\s[4]‥\w5‥\w5時計合わせ失敗。\w8\1なにしてんだよ。\e"
}


//******************************************************************************
//そのほか
//******************************************************************************

//------------------------------------------------------------------------------
//ディスプレイ解像度変更
//------------------------------------------------------------------------------
OnDisplayChange
{
	//---- 変数にディスプレイ解像度と色深度に関する情報を取得
	//---- 使うかどうかは分からないがとりあえず保持しておく
	displaybpp    = reference[0]
	displaywidth  = reference[1]
	displayheight = reference[2]
	SHIORI3FW.RegisterTempVar('displaybpp', 'displaywidth', 'displayheight')
}

//------------------------------------------------------------------------------
//URLドロップ
//CROW でゴーストインストーラからゴーストをインストールを始めた時にも呼ばれます
//------------------------------------------------------------------------------
OnURLDropping
{
	"\0\s[0]%(reference[0])からデータを取得してるよ。\e"
}

//------------------------------------------------------------------------------
//起動時に MATERIA を検出
//------------------------------------------------------------------------------
OnEmbryoExist
{
	"\0\s[0]あれ、「伺か」が起動してるよ。\w8\1ありがたやありがたや。\w8\0\s[4]\n\nなんで拝むの？\e"
}

//------------------------------------------------------------------------------
//起動時に猫どりふを検出
//------------------------------------------------------------------------------
OnNekodorifExist
{
	"\0\s[2]うわ、猫どりふだ‥\w5‥\w5\w8\1\s[11]まだあったのか！\w8\0\s[4]\n\nえ、そこが問題なの？\e"
}

//------------------------------------------------------------------------------
//スクリーンセーバー実行開始
//------------------------------------------------------------------------------
OnScreenSaverStart
{
	"\0\s[5]画面の焼き付きを保護する開始っ！\w8\e"
}

//------------------------------------------------------------------------------
//スクリーンセーバー実行終了
//------------------------------------------------------------------------------
OnScreenSaverEnd
{
	"\1\s[10]‥\w5‥\w5ややめ、起きろ。\w9\nスクリーンセーバーが無くなったぞ。\w8\0\s[2]わ、いつの間に‥\w5‥\w5\e"
}
