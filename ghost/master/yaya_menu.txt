//******************************************************************************
// YAYA　ゴーストテンプレート
// メニュー処理辞書
//******************************************************************************

//******************************************************************************
//メインメニューの展開
//
//AYATEMPLATE.MenuItemの使い方
//1つめの文字列＝選択肢に表示する文字列
//2つめの文字列＝選んだときに実行される関数名
//3つめの文字列＝改行する量　50%刻み　省略すると100%（＝\n）
//******************************************************************************

//------------------------------------------------------------------------------
//メインメニュー展開
//------------------------------------------------------------------------------
OpenMenu
{
	"\0\s[5]はい、なんでしょうか？"
	"\0\s[5]%username、なんですか？"
	"\0\s[0]ん‥‥どうしたの？"
	--
	AYATEMPLATE.MenuItem("なんか話して","Menu_AITALK",200)
	--
	if ( STRLEN(AYATEMPLATE.LastTalk) > 0 ) {
		AYATEMPLATE.MenuItem("さっきの話は？","Menu_LASTTALK")
	}
	--
	AYATEMPLATE.MenuItem("しゃべり頻度を変えて","Menu_TALKINTERVAL")
	--
	AYATEMPLATE.MenuItem("他のゴーストと話す","Menu_GHOSTCOMSTART")
	--
	AYATEMPLATE.MenuItem("ややめに話しかける","Menu_COMMUNICATEOPEN")
	--
	//スタンプ帳チェック
	if AYATEMPLATE.IsPluginExist('379E0DA0-131C-11DD-E1AC-90009E101CFE') {
		AYATEMPLATE.MenuItem("スタンプを押す","Menu_STAMP")
	}
	--
	AYATEMPLATE.MenuItem("なんでもない","Menu_CANCEL",150)
}

//------------------------------------------------------------------------------
//メニュー選択タイムアウト
//------------------------------------------------------------------------------
OnChoiceTimeout
{
	"\0\s[7]ちゃんと選んでくださいっ！\e"
	"\0\s[4]ぷー。\w8\1落ち込むんじゃない。\e"
	"\0\s[4]一定時間が過ぎると。\w8\1キャンセルされるんだよな？\w8\0\n\n自爆します。\w8\1\s[11]\n\nえ！？\w8\0\n\nマック朗が。\w8\1\n\nいや―――――――ッ！！\e"
}


//******************************************************************************
//各項目の処理内容
//******************************************************************************

//---- 何か話して
Menu_AITALK
{
	//---- 明示的にAIトーク開始を指示
	OnAiTalk
}

//---- さっきの話は
Menu_LASTTALK
{
	AYATEMPLATE.LastTalk
}

//---- トーク頻度の調整
Menu_TALKINTERVAL
{
	"\0\s[0]どのくらいがいいのかな？"
	--
	AYATEMPLATE.MenuItem("うるさいくらい","Menu_TALKMAX",200)
	--
	AYATEMPLATE.MenuItem("ふつう","Menu_TALKNORMAL")
	--
	AYATEMPLATE.MenuItem("静か","Menu_TALKPOOR")
	--
	AYATEMPLATE.MenuItem("黙ってて","Menu_TALKNONE")
	--
	AYATEMPLATE.MenuItem("戻る","OpenMenu",150)
}

//---- 文について説明
Menu_ABOUTAYA
{
	"\0\s[0]わたしたちは、汎用DLL「文」の進化版、「YAYA」で制作されてるんだよね？\w8\1聞くなよ。その通りだ。\w9\nＣ言語風の読みやすいスクリプトで、SHIORIやSAORIを作ることが出来るんだ。\w8\0\s[5]\n\n最初は訳分かんないかもしれないけど、噛めば噛むほど面白くなるよ。\w8\s[4]\n多分。\w8\1\n\nおい。\e"
}

//---- ややめに話しかける
Menu_COMMUNICATEOPEN
{
	"\0\s[5]はいっ。\![open,communicatebox]\e"
}

//---- 他のゴーストと話す
Menu_GHOSTCOMSTART
{
	StartCommunicate
}

//---- キャンセルされた
Menu_CANCEL
{
	"\0\s[4]‥\w6‥\w6ぷー。\e"
}

//---- トーク頻度
Menu_TALKMAX
{
	//---- うるさく話す(15秒間隔)
	aitalkinterval = 15
	"\0\s[5]ネタ切れを恐れずがんがん喋る開始っ！\w8\1‥\w5‥\w5待て。\e"
}

Menu_TALKNORMAL
{
	//---- ふつうに話す(3分間隔)
	aitalkinterval = 180
	"\0\s[6]ごく一般的に３分間隔くらいで喋ってみるテスト。\w8\1テストはローカルで。\w8\0\s[4]\n\n‥\w5‥\w5それはどこのルール？\e"
}

Menu_TALKPOOR
{
	//---- 静かに話す(5分間隔)
	aitalkinterval = 300
	"\0\s[5]猫被ります。\w8\1普段から被ってるだろ。\e"
}

Menu_TALKNONE
{
	//---- 話さない
	aitalkinterval = 0
	"\0\s[4]‥\w5‥\w5デスクトップアクセサリーとしてのほとんどの機能を封印して淡々とメモリを消費する作業に入ります。\w8\1なんつー言い方だ。\e"
}

//---- ヘッドライン
Menu_HEADLINECANCEL
{
	//---- キャンセルされた
	"\0\s[5]はーい。\e"
}

//---- スタンプ帳
Menu_STAMP
{
	//初期化
	SHIORI3FW.RegisterTempVar('StampInfo_IsExist','StampInfo_IsNotify','StampInfo_CurrentStampFile','StampInfo_CurrentStampTitle')
	
	StampInfo_IsExist = 0
	StampInfo_IsNotify = 0
	
	//スタンプ帳情報を取った後、raiseでOnStampMenuExecを発生させて下に処理を移す
	"\![raiseplugin,379E0DA0-131C-11DD-E1AC-90009E101CFE,OnStampInfoCall]\![raise,OnStampMenuExec]"
}

OnStampInfo
{
	//スタンプあるかどうか？
	StampInfo_IsExist = reference[1]
	StampInfo_IsNotify = 1
	StampInfo_CurrentStampFile = ''
	StampInfo_CurrentStampTitle = ''
	
	//押している場合は、どのスタンプを押しているか？
	_stamparray = SPLIT(reference[3],CHR(2))
	
	StampInfo_Data = _stamparray
	
	_n = ARRAYSIZE(_stamparray)
	for _i = 0 ; _i < _n ; _i++ {
		_stamp = RE_SPLIT(_stamparray[_i],'[\x1,]') //バイト値1かカンマかでぶった切る：互換モード対策
		
		//ほかで使う場合はここの名前を変えてね
		if _stamp[0] == 'はろーYAYAわーるど' || _stamp[0] == '紺野ややめ' {
			StampInfo_CurrentStampFile = _stamp[1]
			StampInfo_CurrentStampTitle = _stamp[2]
			break
		}
	}
}

OnStampMenuExec
{
	_stamp_type = ''
	
	//あった
	if StampInfo_IsExist {
		if 'ややめ' _in_ StampInfo_CurrentStampTitle { //マック朗におきかえ
			"\u\s[10]\h\s[2]もう押してあるのに、\w5まだほしいの？\w9\w9\u\s[11]ならおれのスタンプに置き換えだ！\w9\nどうだうれしいだろ！\w9\w9\h\s[4]\n\nなにやってるの\w5…\w5…"
			
			_stamp_type = 'マック朗'
		}
		else { //ややめに置き換え
			"\h\s[0]\u\s[10]もうおれのスタンプが押してあるぞ？\w9\w9\h\s[5]とりあえずわたしのに戻すねー。"
			
			_stamp_type = 'ややめ'
		}
	}
	//ない
	else {
		"\0\s[0]スタンプ？\w9\w9\n\s[5]はいどうぞ～。\w9\w9\1\s[11]せっかくだから俺はこのスタンプを選ぶぜ！\w9\w9\0\s[4]えーっと。"
		
		_stamp_type = 'ややめ'
	}
	--
	if _stamp_type == 'ややめ' {
		//2行目ファイル、3行目タイトル、4行目コメント
		"\![raiseplugin,379E0DA0-131C-11DD-E1AC-90009E101CFE,OnStampAdd,/
		ghost/master/stmp_yym.png,/
		ややめスタンプ,/
		紺野ややめが押したスタンプ。服のジッパーの取っ手とデザインが同じ。]"
	}
	else {
		"\![raiseplugin,379E0DA0-131C-11DD-E1AC-90009E101CFE,OnStampAdd,/
		ghost/master/stmp_mcr.png,/
		マック朗スタンプ,/
		マック朗が押したスタンプ。黒林檎。見た目に反して毒はない…と信じたい。]"
	}
}
